# Copyright 2019-2023 Free Software Foundation, Inc.

# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
# Tests GDBs support for OpenMP, for making infcalls inside a
# parallel_for kernel.

load_lib openmp.exp

set device_list [omp_get_device_list]
if {[llength ${device_list}] == 0} {
    unsupported "target does not support OpenMP"
    return 0
}

foreach extension ${openmp_languages_list} {
    standard_testfile "parallel-for-1D.${extension}"
    set exe "${binfile}_${extension}"

    if {${extension} eq "cpp"} {
	set language "c++"
    } else {
	set language ${extension}
    }

    if {[build_executable "failed to compile ${srcfile}" \
	${exe} ${srcfile} [concat openmp ${language} debug]]} {
	continue
    }

    foreach device_info ${device_list} {
	with_test_prefix \
	    [string cat [init_openmp_device ${device_info}] ":${extension}"] {

	    clean_restart ${exe}
	    if {![openmp_run_to_main ${extension}]} {
		untested "Failed to run to main"
		continue
	    }

	    set last_line [gdb_get_line_number "kernel-last-line"]
	    gdb_breakpoint ${last_line}

	    # Check that we can make an infcall from different threads.

	    for {set j 1} {${j} <= 4} {incr j} {
		gdb_continue_to_breakpoint "hit the last line, occurrence ${j}" \
		    ".*${srcfile}:${last_line}.*"

		if {${extension} eq "f90"} {
		    if {[require_openmp_device "GPU"]} {
			setup_kfail "JIRA CMPLRLIBS/34753" "*-*-*"
		    }
		    gdb_test "print update_val\(in_elem, 100\).eqv.in_elem2" ".TRUE."\
			"thread invariant, ${j}"
		} else {
		    if {[require_openmp_device "GPU"]} {
			setup_kfail "JIRA GSD/7293" "*-*-*"
		    }
		    gdb_test "print update_val\(in_elem, 100\) == in_elem2" "true"\
			"thread invariant, ${j}"
		}
	    }
	}
    }
}
