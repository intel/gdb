# Copyright 2019-2022 Free Software Foundation, Inc.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#
# Support library for testing SYCL GDB features
#
# A particular SYCL device can be selected by passing the SYCL program
# two command-line arguments:
#   1. the device type, whose value is in {host, cpu, gpu, accelerator}.
#   2. a substring of the device name.
#
# To set these arguments properly, use a SYCL board file, and
# make your test program select a queue via the get_sycl_queue
# function in gdb.sycl/sycl-util.cpp.  See gdb.sycl/sycl-hello.cpp
# for a sample SYCL program setup.

# Define global variables for the driver version and platform.

load_lib mi-support.exp

if {![info exists sycl_driver_version]} {
    set sycl_driver_version ""
}
if {![info exists sycl_driver_platform]} {
    set sycl_driver_platform ""
}
if {![info exists sycl_selected_device]} {
    set sycl_selected_device ""
}

# Return compiler flags for SYCL.

proc sycl_compilation_options {options} {
    set flags {optimize=-O0}
    return [concat {c++ debug} $options $flags]
}

# Compile a given SYCL source file.

proc prepare_sycl_for_testing {binfile srcfile {options ""}} {
    set options [sycl_compilation_options $options]
    set message "failed to compile $srcfile"
    return [prepare_for_testing $message $binfile $srcfile $options]
}

# Start the process by passing the SYCL device type and a
# substring of the device name as the command line arguments.

proc start_sycl_program {} {
    global device_type
    global device_name
    global gdb_prompt

    if {![info exists device_type] || ![info exists device_name]} {
	verbose "SYCL: test parameters not set; use a SYCL board file" 0
	return -1
    }

    # Because we're doing an internal setup, use gdb_test_multiple
    # instead of gdb_test to not include this as a pass.
    set set_args_cmd "set args '$device_type' '$device_name'"
    gdb_test_multiple $set_args_cmd $set_args_cmd {
	-re "$gdb_prompt $" { }
    }

    if {![runto_main]} {
	untested "failed to run to main"
	return -1
    }

    return 0
}

# Start SYCL program using MI commands.

proc mi_start_sycl_program {} {
    global device_type
    global device_name
    global mi_gdb_prompt

    if {![info exists device_type] || ![info exists device_name]} {
	verbose "SYCL: test parameters not set; use a SYCL board file" 0
	return -1
    }

    # Because we're doing an internal setup, use send_gdb and gdb_expect
    # instead of mi_gdb_test to not include this as a pass.
    send_gdb "-gdb-set args '$device_type' '$device_name'\n"
    gdb_expect 10 {
	-re "\\^done.*$mi_gdb_prompt$" { }
    }

    if {[mi_runto_main] == -1} {
	untested "failed to run to main"
	return -1
    }

    return 0
}

# Return true if the SYCL device selected via the board file
# matches the arguments.  Otherwise return false.
# For the device type, exact match is used.  For the device name,
# pattern-matching is used.

proc require_sycl_device {type name} {
    global device_type
    global device_name

    if {![info exists device_type] || ![info exists device_name]} {
	verbose "SYCL: test parameters not set; use a SYCL board file" 0
	return 0
    }

    set type_match [expr {$device_type eq $type}]
    set name_match [string match $name $device_name]

    return [expr $type_match && $name_match]
}

# Run a test on the target to check if it recognizes SYCL.
# Return 0 if SYCL is supported, 1 otherwise.

gdb_caching_proc skip_sycl_tests {
    global srcdir objdir subdir gdb_prompt
    global inferior_exited_re
    global sycl_selected_device
    global sycl_driver_version
    global sycl_driver_platform
    global device_type device_name

    if {![info exists device_type] || ![info exists device_name]} {
	verbose -log "SYCL: test parameters not set; use a SYCL board file"
	return 1
    }

    # Set up, compile, and execute a simple SYCL program.
    set exe "sycl-hello"
    set src "$srcdir/lib/sycl-hello.cpp"

    if {[prepare_sycl_for_testing $exe $src]} {
	verbose "SYCL: Compilation failed" 0
	return 1
    }
    verbose -log "\nSYCL: Compilation succeeded"

    if {[start_sycl_program] < 0} {
	return 1
    }

    set result 1
    gdb_test_multiple "continue" "continue" {
	-re "SYCL: Using device: \\\[(\[^\r\n\]*)\\\] from \\\[(\[^\r\n\]*)\\\] version \\\[(\[^\r\n\]*)\\\]\r\n" {
	    set dev $expect_out(1,string)
	    set platform $expect_out(2,string)
	    set version $expect_out(3,string)
	    verbose "SYCL: Selected device is '${dev}' from '${platform}' version '${version}'" 0
	    set sycl_selected_device $dev
	    set sycl_driver_version $version
	    set sycl_driver_platform $platform
	    exp_continue
	}
	-re -wrap "$inferior_exited_re normally].*" {
	    set result 0
	}
	-re -wrap "$inferior_exited_re with code.*" {
	    set result 1
	}
    }

    if {$result == 0} {
	verbose "SYCL: Support detected" 0
    } else {
	verbose "SYCL: Support not detected" 0
    }

    gdb_exit

    return $result
}

# Return non-zero, if we are runing at Level-Zero.

proc is_level_zero {} {
    global sycl_driver_platform
    return [regexp {.*(Level-Zero)} $sycl_driver_platform]
}

# Return non-zero, if we are runing on OpenCL.

proc is_opencl {} {
    global sycl_driver_platform
    return [regexp {.*(OpenCL)} $sycl_driver_platform]
}

# Return non-zero if the device is Gen9.

gdb_caching_proc is_gen9 {
    global sycl_selected_device

    if {[string match "Intel*Gen9*" $sycl_selected_device]
	|| [string match "Intel*Iris*Graphics*" $sycl_selected_device]} {
	return 1;
    }

    return 0;
}

# Return non-zero if the device is Gen12.

gdb_caching_proc is_gen12 {
    global sycl_selected_device

    if {[string match "Intel*Gen12*" $sycl_selected_device]
	|| [string match "Intel*Xe MAX Graphics*" $sycl_selected_device]
	|| [string match "Intel*Xe Graphics*" $sycl_selected_device]
	|| [string match "Intel*UHD Graphics 770*" $sycl_selected_device]} {
	return 1;
    }

    return 0;
}

# Return non-zero if the device is ADL.

gdb_caching_proc is_gen12adl {
    global sycl_selected_device

    # Device ids for ADL begins with 0x468 or 0x469.
    if {[is_gen12]
	&& ([string match "*0x468*" $sycl_selected_device]
	    || [string match "*0x469*" $sycl_selected_device])} {
	return 1;
    }

    return 0;
}

# Issue a 'continue' command and expect to see program termination.

proc continue_sycl_program_to_termination {{msg_suffix ""}} {
    global gdb_prompt inferior_exited_re

    set is_non_stop 0
    gdb_test_multiple "show non-stop" "get non-stop mode" {
	-re "non-stop mode is off.\r\n$gdb_prompt $" {
	    set is_non_stop 0
	}
	-re "non-stop mode is on.\r\n$gdb_prompt $" {
	    set is_non_stop 1
	}
    }

    # On ADL, following tests may fail with missing gdb-prompt.
    if {[is_gen12adl]} {
	setup_kfail "JIRA DOQG/2854" "*-*-*"
    }
    if {!$is_non_stop} {
	gdb_test "continue" "$inferior_exited_re normally].*" \
	    "continue to end $msg_suffix"
    } else {
	gdb_test_multiple "continue -a" "continue to end $msg_suffix" {
	    -re "$inferior_exited_re normally].*$gdb_prompt.*" {
		pass $gdb_test_name
	    }
	}
    }
}

# Return the ID of a current thread (<inferior number>.<thread number>).
# SIMD lane information is omitted.
# For thread 2.3:0 returns "2.3".
# For thread 3:5 returns "3".
proc get_current_thread {location} {
    global gdb_prompt decimal

    gdb_test_multiple "thread" "get current thread at $location" {
	-re "Current thread is ($decimal|$decimal\.$decimal).*$gdb_prompt $" {
	    pass $gdb_test_name
	    return $expect_out(1,string)
	}
	-re "$gdb_prompt $" {
	    fail $gdb_test_name
	}
    }
    return 0
}

# Returns a non-negative integer value of variable VAR for SIMD lane
# LANE_NUM in the current thread.
# Returns -1 on error.
proc get_var_for_lane {var {lane_num 0}} {
    global decimal
    global gdb_prompt

    gdb_test "thread :$lane_num" ":$lane_num.*" \
	"switch the current thread to lane $lane_num"

    set msg "get $var value for the current thread, lane $lane_num"
    gdb_test_multiple "print $var" $msg {
	-re "= ($decimal).*$gdb_prompt" {
	    pass $gdb_test_name
	    return $expect_out(1,string)
	}
	-re "$gdb_prompt" {
	    fail $gdb_test_name
	    return -1
	}
    }
}

# Returns 1 if the current thread has SIMD lanes, 0 if it doesn't.
# Returns -1 on error.

proc current_thread_has_simd_lanes {} {
    global gdb_prompt decimal

    gdb_test_multiple "thread" "current_thread_has_simd_lanes" {
	-re "Current thread is $decimal.$decimal:$decimal .*$gdb_prompt $" {
	    return 1
	}
	-re "Current thread is $decimal:$decimal .*$gdb_prompt $" {
	    return 1
	}
	-re "Current thread is .*$gdb_prompt $" {
	    return 0
	}
	-re "$gdb_prompt $" {
	    fail $gdb_test_name
	    return 0
	}
    }

    fail $gdb_test_name
    return 0
}

# Returns the ID of the first stopped thread at $srcfile:$line that is not
# among ids_to_ignore. The returned thread ID is in the format of
# (<inferior number>.<thread number>) and SIMD lane information is omitted.
# For thread 2.3:0 returns "2.3".
# Returns "invalid" if no thread ID is found with given parameters.
proc get_another_stopped_thread {srcfile line ids_to_ignore {msg ""}} {
    global gdb_prompt decimal

    if [string match "" $msg] {
	set msg "get another stopped thread"
    }

    set another_stopped_thread "invalid"
    gdb_test_multiple "info threads" $msg -lbl {
	-re "\r\n. ($decimal\(\.$decimal\)?)\[^\r\n\]* at $srcfile:$line" {
	    if {$another_stopped_thread eq "invalid"} {
		set next_thread $expect_out(1,string)
		if {[lsearch $ids_to_ignore $next_thread] == -1} {
		    set another_stopped_thread $next_thread
		}
	    }
	    exp_continue
	}
	-re "$gdb_prompt $" {
	    gdb_assert { { $another_stopped_thread ne "invalid" } } $gdb_test_name
	}
    }

    return $another_stopped_thread
}
